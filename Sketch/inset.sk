def bubble_crosssection
{
   def cross1 (0,1,1)
   def cross2 (0,1,-1)
   def cross3 (0,-1,-1)
   def cross4 (0,-1,1)
   polygon[cull=false,fill=none](cross1)(cross2)(cross3)(cross4)
   
   %{crosssection}
   def radius 0.4
   def left1 (0.0,0.8-radius,-0.8)
   def left2 (0.0,-0.8+radius,-0.8)
   def right1 (0.0,0.8-radius,0.8)
   def right2 (0.0,-0.8+radius,0.8)
   def top1 (0.0,0.8,-0.8+radius)
   def top2 (0.0,0.8,0.8-radius)
   def bottom1 (0.0,-0.8,-0.8+radius)
   def bottom2 (0.0,-0.8,0.8-radius)
   line (left1)(left2)
   line (right1)(right2)
   line (top1)(top2)
   line (bottom1)(bottom2)
   line[line style=dashed] (0,0,0)(0,1.25,1.25)
   line[line style=dashed] (0,1.25,1.25)(0,1.25,1.5)
   line[line style=dashed] (0,0,0)(0,0,-1.5)
   line[arrows=->,line width=1pt] (0,0,0)(0,0,-0.8)
   line[arrows=->,line width=1pt] (0,0,0)(0,0.8-radius*(1.0-0.707106),0.8-radius*(1.0-0.707106))
   def number_segs 10
   def arc{
      sweep{ number_segs, rotate(90/number_segs, [-1,0,0]) } 
      (0,0,radius)
   }
   put{translate([0,0.8-radius,0.8-radius])}{arc}
   put{rotate(90,[1,0,0]) then translate([0,-0.8+radius,0.8-radius])}{arc}
   put{rotate(180,[1,0,0]) then translate([0,-0.8+radius,-0.8+radius])}{arc}
   put{rotate(270,[1,0,0]) then translate([0,0.8-radius,-0.8+radius])}{arc}
    
   special |\node at #1 {$r_{d}$};|[lay=in]
         (0,1.4,1.5)
   #special |\draw #1 -- #2 node[sloped,above,midway] {$R_{diag}$};|
   #      (0,1.25,1.25)(0,1.25,2.0)  
   special |\node at #1 {$r_{h}$};|[lay=in]
         (0,0.15,-1.3)

}
put {rotate(90,[0,1,0])}{bubble_crosssection}
%{bubble_crosssection}

global { language tikz }
